plugins {
    id 'java'
    id 'antlr'
    id 'idea'
    // https://plugins.gradle.org/plugin/org.fulib.fulibGradle
    id 'org.fulib.fulibGradle' version '0.5.0'
}

group 'org.fulib'
version '1.0.0'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.antlr/ST4
    implementation group: 'org.antlr', name: 'ST4', version: '4.3.1'

    // https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
    implementation group: 'org.antlr', name: 'antlr4-runtime', version: '4.9.3'

    // https://mvnrepository.com/artifact/org.fulib/fulibScenarios
    fulibScenarios group: 'org.fulib', name: 'fulibScenarios', version: '1.7.0'

    // https://mvnrepository.com/artifact/org.fulib/fulibTools
    implementation group: 'org.fulib', name: 'fulibTools', version: '1.5.1'

    // https://mvnrepository.com/artifact/org.fulib/fulibTables
    testImplementation group: 'org.fulib', name: 'fulibTables', version: '1.4.0'

    // https://mvnrepository.com/artifact/org.fulib/fulibMockups
    testImplementation group: 'org.fulib', name: 'fulibMockups', version: '0.4.0'

    // https://mvnrepository.com/artifact/junit/junit
    testImplementation group: 'junit', name: 'junit', version: '4.13'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-nop
    testImplementation group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.30'

    antlr 'org.antlr:antlr4:4.9.3'
}

generateScenarioSource {
    classDiagramSVG = true
}

// Source: https://tomassetti.me/antlr-mega-tutorial/#chapter18
generateGrammarSource {
    maxHeapSize = "128m"
    arguments += ['-package', 'org.fulib.workflows.yaml', '-no-visitor', '-listener', '-encoding', 'UTF-8']
}
compileJava.dependsOn generateGrammarSource
sourceSets {
    generated {
        java.srcDir 'generated-src/antlr/main/'
    }
}
compileJava.source sourceSets.generated.java, sourceSets.main.java
clean{
    delete "generated-src"
}
idea {
    module {
        sourceDirs += file("generated-src/antlr/main/")
    }
}